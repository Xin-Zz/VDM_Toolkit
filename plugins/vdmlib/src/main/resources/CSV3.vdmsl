module CSV3
imports 
    from VDMUtil 
        functions 
            val2seq_of_char renamed val2seq_of_char;
    ,
    from IO
        operations 
            printf renamed printf;
exports 
    types
        struct String;
        struct String1;
        struct Path;
        struct FileStatus;
        struct Header;
        struct Headers0;
        struct Headers;
        struct Line;
        struct Col;
        struct Row;
        struct Rows;
        struct Data0;
        struct Data;
        struct CSVType;
        struct CSVTypeInv;
        struct CSVRowInv;
        struct CSVValue; 
        struct CSVParser;
    values 
        HEADER_INVARIANT_TRUE: CSVTypeInv;
        ROW_INVARIANT_TRUE   : CSVRowInv;
        EMPTY_ROWS           : Rows;
        EMPTY_CSV            : Data;
        DEFAULT_HEADER_NAME  : String1;
    functions 
        --@doc functions to process CSV data IO 
        file_status          : Path -> FileStatus;
        csv_read_data        : Path * CSVParser * Headers0 -> bool * Data0;
        csv_write_data       : Path * Data -> bool;
        --@doc functions on basic row and cell consistency
        row_consistency      : Headers0 * Row -> bool;
        csv_type_check       : CSVType * CSVValue -> bool;
        --@doc functions identifying (and purging) invalid line/columns
        csv_invariants_failed: Data0 -> set of (Line * Col);
        csv_filter_invalid   : Data0 -> Data;
        --@doc construction functions for empty and updated row invariant data
        empty_csv            : Headers -> Data;
        install_row_invariant: Data0 * CSVRowInv -> Data0;
    operations
        --@doc Loads CSV data from a given file with given parser back-end choice and
        --     specific headers. If trivial load, no invariants and just list of header types.
        --     Any IO (or VDM Value) erros can be shown if they occur.
        showError     : () ==> ();
        loadCSV       : Path * CSVParser * Headers0 ==> ();
        loadTrivialCSV: Path * CSVParser * seq1 of CSVType ==> ();
definitions
types
    String = seq of char;

    String1 = seq1 of char;

    --@doc this is treated specially by the native methods. It will be 
    --     sensitive to '/' (Linux) and '\' (Windows) path separators
    --     as well as absolute (/path) or relative (./path) files names. 
    Path = String1; 

    FileStatus = <Valid> | <DoesNotExist> | <CannotBeRead> | <CannotBeWritten> | <IsDirectory>; 

    --@todo any more types needed? @AB?
    CSVType = <Integer> | <Float> | <String>; 

    CSVValue = int | real | String;

    --@doc CSV parser IO back-end choices. Native is a variation of an implemention as a state 
    --     machine inspired online. Other implementations are linked below. They vary in clarity,
    --     speed, dependenecies, and variety of CSV formats. 
    --          Native    = https://stackoverflow.com/questions/6857248/fast-csv-parsing
    --          Apache    = https://commons.apache.org/proper/commons-csv/
    --          Univocity = https://github.com/uniVocity/univocity-parsers
    --          OpenCSV   = https://opencsv.sourceforge.net
    --          QuirkCSV  = https://github.com/bpoole6/quirk-csv
    --     
    --@todo is this too much? (i.e. many dependencies; presuming only <Native> for now)
    CSVParser = <Native> | <Apache> | <Univocity> | <OpenCSV> | <QuirkCSV>; 

    --@doc an invariant function that checks CSVValue against
    --     corresponding types.
    CSVTypeInv = CSVType * CSVValue -> bool;

    --@doc an invariant function that checks the whole row of values against the header
    CSVRowInv = (Headers0 * Row -> bool);

    --@doc each header contain a name, type and invariant.
    --     it represents a CSV column with (a "vertical") 
    --     invariant (if any) across all its rows for each column (i.e. all cells).  
    Header :: 
        name: [String1]
        type: CSVType
        invariant: [CSVTypeInv];

    --@doc Raw header list representing expected types and
    --     their invariants (possibly empty), where names 
    --     might be file/data dependant (i.e. will come from)
    --     a native call.
    Headers0 = seq of Header;

    --@doc Final header, where names have been loaded from 
    --     native call. 
    Headers = Headers0
    inv h == h <> [] and (forall mk_Header(n, -, -) in set elems h & n <> nil);

    --@doc a row is a sequence of values
    Row = seq of CSVValue;

    --@doc collection of CSV rows corresponds to a sequence of Row and a row invariant. 
    Rows :: 
        cells: seq of Row
        invariant: [CSVRowInv];

    --@doc CSV line
    Line = nat1;

    --@doc CSV col
    Col = nat1;

    --@doc CSV data its raw headers and sequence of its rows
    --     (i.e. effectively the CSV matrix), as in:  
    --
    --       H1   |   H2   ... |   Hj
    -- R1  v(1,1) | v(1,2) ... | v(1,j)
    -- ...
    -- Ri  v(i,1) | v(i,2) ... | v(i,j)
    --
    Data0 :: 
        headers: Headers0
        rows   : Rows
    inv mk_Data0(h, r) == 
        --@doc if data is available, check that all headers have names
        (h <> [] => is_Headers(h))
        and
        line_col_size_consistency(h, r);
    
    Data = Data0 
    inv d ==
        --@doc bit expensive invariant check across the whole CSV data:
        --     for all rows d(i), check all header invariants h(j) for
        --     all cells d(i)(j)
        --@NB --@AB views?
        --@todo arguably could have just row invariant, which also checks for cell invariants? 
        csv_invariants_failed(d) = {};

values 
    HEADER_INVARIANT_TRUE: CSVTypeInv = (lambda t: CSVType, v: CSVValue & csv_type_check(t, v));
    ROW_INVARIANT_TRUE   : CSVRowInv  = (lambda h: Headers0, r: Row & row_consistency(h, r));
    EMPTY_ROWS           : Rows       = mk_Rows([], nil);
    EMPTY_CSV            : Data       = mk_Data0([], EMPTY_ROWS);
    DEFAULT_HEADER_NAME  : String1    = "CSVHeader";

functions 

    --@doc every row of data must match the expected header's length
    --     because headers might also be just names, have the @T for either
    line_col_size_consistency: Headers0 * Rows -> bool
    line_col_size_consistency(headers, mk_Rows(cells, -)) == 
        (forall i in set inds cells & row_consistency(headers, cells(i)));
        
    --@doc checks whether given row has the same sime as headers (e.g. no short rows) 
    row_consistency: Headers0 * Row -> bool 
    row_consistency(headers, row) ==
        len headers = len row; 

    --@doc checks installed (possibly nameless) headers match resulting headers
    --@private used by operations to ensure CSV IO header read doesn't change 
    --         user defined types and invariants     
    installed_read_headers_consistency: Headers0 * Headers -> bool 
    installed_read_headers_consistency(h, h') ==
        len h = len h' 
        and 
        --@doc headers are the same, except for the names
        (forall i in set inds h & 
            h(i).type = h'(i).type 
            and 
            --@Warning(5037) here just checking structural equality rather than computation.
            (h(i).invariant = h'(i).invariant)); 

    --@doc creates empty CSV data with expected headers
    empty_csv: Headers -> Data
    empty_csv(headers) == mk_Data0(headers, EMPTY_ROWS);
        
    --@doc checks the full path file status.
    file_status: Path -> FileStatus 
    file_status(path) == is not yet specified; 

    --@doc Reads CSV data. Usually header names are in the first line of the CSV file;
    --     empty names is allowed (i.e anonynous headers). Such headers can have invariants.
    --     Result is Data0, so that the invariants will be checked if no violations.
    --
    --     Actual CSV data comes next as a sequence of rows (i.e. CSV matrix parser).
    --     CSV parsing compliance is tricky, as there are many formats out there.
    --     e.g. https://commons.apache.org/proper/commons-csv/
    --     as well as many varied implementations (see CSVParser type).
    --
    --     We want a solution that is fast, so we checked options within  
    --     https://github.com/uniVocity/csv-parsers-comparison
    --
    --     We also want a solution with least dependencies, so were tempted with
    --     Iterable stand alone version in https://stackoverflow.com/questions/6857248/fast-csv-parsing  
    --
    --     Other useful features might be interesting, so we considered filterd CSV from
    --     https://stackoverflow.com/questions/61250600/univocity-csv-parser-skip-rows-based-on-conditions
    csv_read_data: Path * CSVParser * Headers0 -> bool * Data0
    csv_read_data(path, parser, headers) == is not yet specified
    pre file_status(path) = <Valid>
    post 
        --@doc if result is true, names size must be consistent for every row; otherwise, all empty
        (let mk_(b, mk_Data0(headers', rows)) = RESULT in
            if b then
                --@doc line/col consistency with respect to given headers
                --     (e.g. data.headers = headers + CSV header names).
                line_col_size_consistency(headers, rows) 
                and
                installed_read_headers_consistency(headers, headers')
            else 
                headers = [] and rows = EMPTY_ROWS
        ); 

    --@doc prints the CSV data to the given file name
    csv_write_data: Path * Data -> bool
    csv_write_data(path, data) == is not yet specified
    pre file_status(path) in set {<Valid>, <DoesNotExist>}
    post (RESULT => file_status(path) = <Valid>);

    --@doc basic CSV types check against values
    csv_type_check: CSVType * CSVValue -> bool
    csv_type_check(t, v) == 
        cases t: 
            <Integer> -> is_int(v),
            <Float>   -> is_real(v),
            <String>  -> is_String(v),
            others    -> false 
        end;

    --@doc removes rows of invariant violating cells  
    csv_filter_invalid: Data0 -> Data
    csv_filter_invalid(data) == 
        --@doc avoid mk_Data0(...) on input checking invariant of Data0 at other places again
        let lcs: set of (Line * Col) = csv_invariants_failed(data) in
            --@doc same headers but filter rows where lines fail
            mk_Data0(
                data.headers,
                mk_Rows(
                    [ data.rows.cells(i) 
                    | 
                    i in set inds data.rows.cells 
                    & 
                    not i in set { l | mk_(l, -) in set lcs }
                    ], 
                    data.rows.invariant
                )
            ) 
    post data.headers = RESULT.headers 
         and 
         elems RESULT.rows.cells subset elems data.rows.cells
         --and
         --@doc already part of Data
         --csv_invariants_failed(RESULT) = {}
         ;

    --@doc changes the Data row invariant
    install_row_invariant: Data0 * CSVRowInv -> Data0
    install_row_invariant(mk_Data0(headers, mk_Rows(cells, -)), invariant) == 
        mk_Data0(headers, mk_Rows(cells, invariant));

    --@doc for given line checks the whole row is consistent 
    --     (i.e. no short rows wrt to header size) and invariant holds (if any)
    row_invariant_check: Headers0 * Rows * Line -> bool
    row_invariant_check(headers, mk_Rows(cells, invariant), line) ==
        row_consistency(headers, cells(line))
        and 
        (invariant <> nil => invariant(headers, cells(line)))
    pre line in set inds cells;

    --@doc for given line and column within row cells, check
    --     types confirm to declared header type and invariant holds (if any)
    cell_invariant_check: Headers0 * Rows * Line * Col -> bool 
    cell_invariant_check(headers, mk_Rows(cells, -), line, col) ==
        let type: CSVType  = headers(col).type, 
            cell: CSVValue = cells(line)(col)
        in
            csv_type_check(type, cell)
            and
            (headers(col).invariant <> nil => headers(col).invariant(type, cell))
    pre line in set inds cells 
        and 
        col in set inds headers 
        and 
        col in set inds cells(line)
        and
        --@todo redundant if not exporeted? or keep for intra-consistency?
        row_consistency(headers, cells(line));

    --@doc collects the Line/Col points where invariants failed. Useful for
    --     telling users where it went wrong; columns somewhat irrelevant for
    --     row filtering. 
    csv_invariants_failed: Data0 -> set of (Line * Col)
    csv_invariants_failed(mk_Data0(headers, rows)) == 
        --@doc For all lines (l) in the rows, check that each column (c) invariant 
        --     in the header holds, for every lement i in rows (e.g. each element of the row).
        --     Returns the set of line/column numbers where invariants are broken. 
        let 
            all_cells: set of (Line * Col) = 
                { mk_(l, c) | l in set inds rows.cells, c in set inds headers },
            checked_cells: set of (Line * Col) = 
                { mk_(l, c) | l in set inds rows.cells, c in set inds headers
                  & 
                  row_invariant_check(headers, rows, l)
                  and
                  cell_invariant_check(headers, rows, l, c)
                }
        in
            all_cells \ checked_cells;
 
--@doc CSV state constains CSV file, which kind of CSV parser to use,
--     any computation (IO and other) erros, user-defined headers, and
--     read valid (invariant checked) CSV data.  
state CSV of 
    file    : [Path]
    parser  : CSVParser
    ferr    : [String1]
    headersI: Headers0
    data    : [Data]
inv mk_CSV(file, -, ferr, headersI, data) ==
        --@doc if there is an error, zero the information
        (ferr <> nil => data = nil)
        and 
        --@doc if file is set, must be valid
        (file <> nil => file_status(file) = <Valid>)
        and
        --@doc installed headers must be consistent with read headers.
        (data <> nil => installed_read_headers_consistency(headersI, data.headers))
init csv == 
        csv = mk_CSV(nil, <Native>, nil, [], nil)
end

operations 

    --@doc returns last IO error or nil if none.
    lastError: () ==> [String1]
    lastError() == is not yet specified;

    --@doc shows the error if the state registered it
    showError() ==
        (if ferr <> nil then 
            --@todo remove to minimise dependency to IO?
            printf("CSV error %1s", [ferr]);
        )
    ext rd ferr;
    
    --@doc sets CSV file path and zeros data, registering/showing any IO error. 
    setCSVFile(path: Path, parserChoice: CSVParser) == 
        (atomic
            (file     := path;
             parser   := parserChoice;
             data     := nil;
             headersI := [];
             ferr     := lastError();
            );
         showError();
        )
    ext wr file, parser, headersI, data, ferr
    pre file_status(path) = <Valid>
    post file = path and file <> nil and 
         parser = parserChoice and data = nil and headersI = [];

    --@doc installs given headers
    installHeaders(invs: Headers0) ==
        (headersI := invs;
        )
    ext rd file, data wr headersI
    pre --@doc file must be loaded, but no data yet
        file <> nil and data = nil;

    --@doc loads CSV data via CSV parser + IO, returing only if valid (invariant holding) data
    loadData() == 
        (let mk_(ok, data') = csv_read_data(file, parser, headersI) in
            if ok then 
            (   --@doc check invariants using teh installed headers against read rows
                let lcs: set of (Line * Col) = csv_invariants_failed(data') in
                    if (lcs = {}) then
                    (
                        --@doc update the headers with names as well, if successful.
                        atomic (
                            data := data';
                            ferr := nil;
                        );
                    )
                    else
                    (
                        atomic (
                            data  := nil;
                            --@todo remove to avoid dependency on VDMUtil?
                            ferr  := "CSV invariant failure at cells \n\t" ^ 
                                val2seq_of_char[set of (Line * Col)](lcs);
                        );
                    )
            )
            else 
            (
                atomic (
                    data := nil;
                    ferr := lastError();
                );
                
            );
            showError();
        )
    ext rd file, parser, headersI wr data, ferr
    pre  --@doc both file and headers have been set. 
         file <> nil and headersI <> []
    post --@doc state type invariant will ensure csv_invariants_failed is empty.              
         (if ferr = nil then 
            data <> nil and 
            --@todo somewhat redundant given state invariant?
            (installed_read_headers_consistency(headersI, data.headers))
            and
            --@todo somewhat redundant given type invariant for Data0? 
            line_col_size_consistency(data.headers, data.rows)
          else 
             data = nil);  

    --@doc top-level call: given a file and typed headers (possibly with invariants)
    --     loads the CSV as part of the state
    --@todo this could also be part of a function returning Data? Then no state etc?
    loadCSV(path: Path, parserChoice: CSVParser, headersToInstall: Headers0) == 
        (
            if file_status(path) <> <Valid> then 
                showError()
            else 
            (
                setCSVFile(path, parserChoice);
                installHeaders(headersToInstall);
                loadData();
            );
        )
    ext wr CSV
    post post_loadData(CSV~, CSV);

    --@doc load file path with given headers and no invariants. 
    --     it insists on minimally knowking the header types. 
    loadTrivialCSV(path: Path, parserChoice: CSVParser, headerTypes: seq1 of CSVType) ==
        (
            let headers: Headers0 = 
                [ mk_Header(
                    DEFAULT_HEADER_NAME ^ val2seq_of_char[nat1](i), 
                    headerTypes(i), 
                    HEADER_INVARIANT_TRUE
                  ) 
                  | 
                  i in set inds headerTypes 
                ] 
            in 
                loadCSV(path, parserChoice, headers);
        )
    ext wr CSV 
    post post_loadData(CSV~, CSV);

end CSV3