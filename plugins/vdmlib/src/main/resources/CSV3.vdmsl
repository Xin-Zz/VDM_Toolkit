module CSV3
imports 
    from VDMUtil 
        functions 
            val2seq_of_char renamed val2seq_of_char;
    ,
    from IO
        operations 
            printf renamed printf;
exports 
    types
        struct String;
        struct String1;
        struct Path;
        struct FileStatus;
        struct Header;
        struct Headers0;
        struct Headers;
        struct Line;
        struct Col;
        struct Row;
        struct Rows;
        struct Data0;
        struct Data;
        struct CSVType;
        struct CSVTypeInv;
        struct CSVValue; 
    functions 
        file_status         : Path -> FileStatus;
        csv_read_headers    : Path -> bool * (seq of String1);
        csv_read_data[@p]   : Path -> bool * [@p];
        csv_invariant_failed: Data0 -> set of (Line * Col);
    operations
        --lastError     : () ==> [String1];
        showError     : () ==> ();
        setCSVFile    : Path ==> ();
        installHeaders: seq1 of (CSVType * CSVTypeInv) ==> ();
        loadCSV       : () ==> ();
definitions
types
    String = seq of char;

    String1 = seq1 of char;

    --@doc this is treated 
    Path = String1; 

    FileStatus = <Valid> | <DoesNotExist>; 

    CSVType = <Integer> | <Float> | <String>; 

    CSVValue = int | real | String;

    --@doc an invariant function that checks CSVValue against
    --     corresponding types.
    CSVTypeInv = CSVValue -> bool;

    Names = seq of String1; 

    --@doc each header contain a name, type and invariant.
    --     it represents a CSV column with (a "vertical") 
    --     invariant across all its rows for each column.  
    Header :: 
        name: [String1]
        type: CSVType
        invariant: CSVTypeInv;

    --@doc Raw header list representing expected types and
    --     their invariants (possibly empty), where names 
    --     might be file/data dependant (i.e. will come from)
    --     a native call.
    Headers0 = seq of Header;

    --@doc Final header, where names have been loaded from 
    --     native call. 
    Headers = Headers0
    inv h == h <> [] and (forall mk_Header(n, -, -) in set elems h & n <> nil);

    --@doc A CSV row corresponds to a sequence of CSVValue. 
    Row = seq of CSVValue;

    Rows = seq of Row;

    --@doc CSV line
    Line = nat1;

    --@doc CSV col
    --@todo perhaps have it as a Pos :: line: nat1 col: nat1;?
    Col = nat1;

    --@doc CSV data its raw headers and sequence of its rows
    --     (i.e. effectively the CSV matrix), as in:  
    --
    --       H1   |   H2   ... |   Hj
    -- R1  v(1,1) | v(1,2) ... | v(1,j)
    -- ...
    -- Ri  v(i,1) | v(i,2) ... | v(i,j)
    --
    Data0 :: 
        headers: Headers0
        --@todo do we need invariants per row? 
        data   : Rows
    inv mk_Data0(h, d) == 
        --@doc if data is available, check that all headers have names
        (h <> [] => is_Headers(h))
        and
        --@doc every row of data must match the expected header's length
        (forall i in set inds d & len d(i) = len h)
        ;
    
    Data = Data0 
    inv d ==
        csv_invariant_failed(d) = {}
        and
        --@doc bit expensive invariant check across the whole CSV data:
        --     for all rows d(i), check all header invariants h(j) for
        --     all cells d(i)(j)
        (forall i in set inds d.data & 
              forall j in set inds d.headers &     
                  d.headers(j).invariant(d.data(i)(j)));

functions 

    --@doc checks the full path file status.
    file_status: Path -> FileStatus 
    file_status(path) == is not yet specified; 

    --@todo needed? or part of csv_read_data? Maybe yes.
    -- csv_read_headers: Path -> bool * (seq of String1)
    -- csv_read_headers(path) == is not yet specified
    -- pre file_status(path) = <Valid>
    -- post let mk_(b, headers) = RESULT in (not b => headers = []);

    --@doc Reads CSV data. Usually header names are in the first line of the CSV file;
    --     empty names is allowed (i.e anonynous headers). Such headers can have invariants.
    --
    --     Actual CSV data comes next as a sequence of rows (i.e. CSV matrix parser).
    --     CSV parsing compliance is tricky, as there are many formats out there.
    --     e.g. https://commons.apache.org/proper/commons-csv/
    --     as well as many varied implementations
    --          https://opencsv.sourceforge.net
    --          https://github.com/bpoole6/quirk-csv
    --          https://github.com/uniVocity/univocity-parsers
    --
    --     We want a solution that is fast, so we checked options within  
    --     https://github.com/uniVocity/csv-parsers-comparison
    --
    --     We also want a solution with least dependencies, so were tempted with
    --     Iterable stand alone version in https://stackoverflow.com/questions/6857248/fast-csv-parsing  
    --
    --     Other useful features might be interesting, so we considered filterd CSV from
    --     https://stackoverflow.com/questions/61250600/univocity-csv-parser-skip-rows-based-on-conditions
    csv_read_data: Path -> bool * Names * Rows
    csv_read_data(path) == is not yet specified
    pre file_status(path) = <Valid>
    post let mk_(b, data) = RESULT in (not b => data = []);

    csv_invariant_failed: Data0 -> set of (Line * Col)
    csv_invariant_failed(mk_Data0(headers, data)) == 
        --@doc For all lines (l) in the data, check that each column (c) invariant 
        --     in the header holds, for every lement i in data (e.g. each element of the row).
        --     Returns the set of line/column numbers where invariants are broken. 
        { 
            mk_(l, c) 
            |  
            l in set inds data, c in set inds headers
            & 
            len data(l) = len headers
            and
            --@doc for every row (l) and column (c), check the data values against
            --     the header's expected invariant to ensure the resulting CSV load
            --     is sound with respect to the defined Headers' invariants. 
            not headers(c).invariant(data(l)(c))
        };

state CSV of 
    file   : [Path]
    ferr   : [String1]
    data   : [Data]
inv mk_CSV(file, ferr, data) ==
        --@doc if there is an error, zero the information
        (ferr <> nil => data = nil)
        and 
        --@doc if file is set, must be valid
        (file <> nil => file_status(file) = <Valid>)
init csv == 
        csv = mk_CSV(nil, nil, nil)
end

operations 

    --@doc returns last IO error or nil if none.
    lastError: () ==> [String1]
    lastError() == is not yet specified;

    --@doc shows the error if the state registered it
    showError() ==
        (if ferr <> nil then 
            printf("CSV error %1s", [ferr]);
        )
    ext rd ferr;
    
    --@doc sets CSV file path and zeros data, registering/showing any IO error. 
    setCSVFile(path: Path) == 
        (atomic
            (file   := path;
             data   := nil;
             ferr   := lastError();
            );
         showError();
        )
    ext wr file, data, ferr
    pre file_status(path) = <Valid>
    post file = path and file <> nil and data = nil;

    installHeaders(invs: seq1 of (CSVType * CSVTypeInv)) ==
        (data.headers := [];
        )
    ext rd file wr data
    pre file <> nil 
    post data.data = data~.data
         and
         data.headers <> data~.headers 
         and 
         len data.headers = len invs;

    loadCSV() == 
        (let mk_(ok, rows) = csv_read[Rows](file) in
            if ok then 
            (
                let lcs: set of (Line * Col) = csv_invariant_failed(mk_Data0(data.headers, rows)) in
                    if (lcs = {}) then
                    (
                        atomic (
                            data.data := rows;
                            ferr      := nil;
                        );
                    )
                    else
                    (
                        --@TODO show errors on LINE * COL SET! 
                        atomic (
                            data.data := [];
                            ferr      := "CSV invariant failure at \n\t" ^ 
                                val2seq_of_char[set of (Line * Col)](lcs);
                        );
                    )
            )
            else 
            (
                atomic (
                    data.data:= [];
                    ferr     := lastError();
                );
                
            );
            showError();
        )
    ext rd file wr data, ferr
    pre  --@doc both file and headers have been set. 
         file <> nil and data.headers <> []
    post (data.headers = data~.headers)
         and
         --@doc state type invariant will ensure csv_invariant_failed is empty.              
         (if ferr = nil then 
             data.data <> data~.data 
          else 
             data.data = []);  

end CSV3